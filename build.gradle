//-*- mode: groovy -*-

plugins {
	id 'application'
	id 'jacoco'
	id 'java'

	id 'com.diffplug.spotless' version '5.15.1'
	id 'com.github.johnrengelman.shadow' version '7.1.0'
	id 'net.nemerosa.versioning' version '2.8.2'
	id 'org.ajoberstar.grgit' version '4.1.0'
	id 'org.panteleyev.jpackageplugin' version '1.3.1'
}
mainClassName = 'net.sourceforge.kolmafia.KoLmafia'

sourceSets {
	main {
		java {
			srcDirs = ['src', 'lib']
			destinationDirectory.set(file('build/main'))
		}
		resources {
			srcDirs = ['src', 'lib']
			excludes = ['**/*.java', '**/*.jar']
		}
	}

	lib {
		java {
			srcDirs = ['lib']
			destinationDirectory.set(file('build/lib'))
		}
	}

	test {
		java {
			srcDirs = ['test']
			destinationDirectory.set(file('build/test'))
		}
	}
}

repositories {
	// Use Maven Central for resolving dependencies.
	mavenCentral()
}

dependencies {
	// Use JUnit Jupiter for running JUnit5 tests.
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.8.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter:5.8.0'

	testImplementation 'org.hamcrest:hamcrest:2.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.0'
	testImplementation 'org.mockito:mockito-core:3.+'

	implementation 'com.formdev:flatlaf:1.6'
	implementation 'com.formdev:flatlaf-intellij-themes:1.6'
	implementation 'com.formdev:flatlaf-swingx:1.6'
	// Optional runtime deps for svnkit
	runtimeOnly 'com.trilead:trilead-ssh2:1.0.0-build222'
	runtimeOnly 'net.java.dev.jna:jna:5.9.0'
	runtimeOnly 'net.java.dev.jna:jna-platform:5.9.0'
	runtimeOnly 'org.antlr:antlr-runtime:3.5.2'

	libImplementation 'org.swinglabs:swingx:1.0'

	implementation files('build/lib') {
		builtBy 'compileLibJava'
	}

	implementation 'net.sourceforge.htmlcleaner:htmlcleaner:2.24'
	implementation 'org.fusesource.jansi:jansi:2.3.4'
	implementation 'org.json:json:20210307'
	implementation 'org.mozilla:rhino:1.7.13'
	implementation 'org.swinglabs:swingx:1.0'
	implementation 'org.tmatesoft.svnkit:svnkit:1.10.3'
}

application {
	// Define the main class for the application.
	mainClass = 'net.sourceforge.kolmafia.KoLmafia'
}

spotless {
	format 'misc', {
		target '.gitignore'

		trimTrailingWhitespace()
		indentWithSpaces(2)
		endWithNewline()
	}

	freshmark {
		target '*.md'
	}

	groovyGradle {
		greclipse()
	}
	java {
		target 'src/**/*.java', 'test/**/*.java'
		googleJavaFormat()
	}
}

task cleanDist(type: Delete) {
	onlyIf {
		file('dist').exists()
	}
	inputs.dir('dist')
	outputs.upToDateWhen { true }
	doLast {
		file('dist').eachFile(groovy.io.FileType.FILES) { File file ->
			if (file.name.startsWith('KoLmafia-') && file.name.endsWith('.jar')) {
				delete file
			}
		}
	}
}

task pruneDist(type: Delete) {
	onlyIf {
		file('dist').exists()
	}
	inputs.dir('dist')
	outputs.upToDateWhen { true }
	doLast {
		file('dist').eachFile(groovy.io.FileType.FILES) { File file ->
			if (file.name.startsWith('KoLmafia-') && file.name.endsWith('.jar')) {
				if (!file.name.contains(project.getVersion().toString()) || (versioning.info.dirty != file.name.endsWith('-M.jar'))) {
					delete file
				}
			}
		}
	}
}

test {
	useJUnitPlatform()

	systemProperty 'line.separator', '\n'
	jvmArgs '-DuseCWDasROOT=true'
	workingDir 'test/root'

	reports {
		html.required = true
		junitXml.required = true
	}
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}

configurations {
	implementation.canBeResolved = true
}

jar {
	manifest {
		attributes(
				'Main-Class'      : 'net.sourceforge.kolmafia.KoLmafia',
				'Build-Revision'  : new Object() { String toString() { project.getVersion() } },
				'Build-Branch'    : versioning.info.branch,
				'Build-Commit'    : versioning.info.commit,
				'Build-Dirty'     : versioning.info.dirty,
				'Build-Jdk'       : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
				'Build-OS'        : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
				)
	}

	from { configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) } } {
		exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
	}
	duplicatesStrategy = 'exclude'
	destinationDirectory = file('dist/')
	archiveBaseName.set('KoLmafia')
	archiveClassifier.set("${versioning.info.dirty ? 'M' : ''}")
}

shadowJar {
	duplicatesStrategy = 'exclude'
	destinationDirectory = file('dist/')
	archiveClassifier.set("${versioning.info.dirty ? 'M' : ''}")
}

task getRevision() {
	onlyIf {
		file('.git').exists()
	}
	def commit = findProperty('commit') ?: 'origin/main'
	inputs.dir('.git')
	inputs.property('commit', commit)
	outputs.files file('build/revision.txt')

	doLast {
		def revision = grgit.log(includes:[commit]).size()
		logger.info('Commit: {} Revision: {}', commit, revision)
		file('build/revision.txt').text = revision.toString().trim()
		// Update the version to the new revision
		project.setVersion(revision.toString().trim())
		def revString = versioning.info.dirty ? project.getVersion() +'-M': project.getVersion()
		println '\nRevision: ' + revString

	}
}

task gitUpdate() {
	doLast {
		def remote = grgit.branch.current().trackingBranch.getName()
		def latestHead = grgit.resolve.toCommit(remote)
		grgit.fetch {
			remote = remote
		}
		if (grgit.resolve.toCommit(remote) == latestHead) {
			println 'Already up-to-date, nothing to do.'
			return
		}
		def dirty = versioning.info.dirty
		if (dirty) {
			// This pollutes the reflog, but there's no stash functionality in
			// grgit...
			grgit.commit {
				message = 'temporary stash commit'
				all = true
			}
		}
		grgit.pull {
			rebase = true
		}
		if (dirty) {
			grgit.reset {
				mode = 'mixed'
				commit = 'HEAD^'
			}
		}
	}
}

java {
	sourceCompatibility = JavaVersion.toVersion(javaSourceCompatibility)
	targetCompatibility = JavaVersion.toVersion(javaTargetCompatibility)
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task cleanJpackage(type: Delete) {
	onlyIf {
		file('build/releases').exists()
	}
	delete 'build/releases'
}

tasks.jpackage {
	dependsOn shadowJar, cleanJpackage
	input = 'dist'
	destination = 'build/releases'
	mainClass = mainClassName
	appVersion = new Date().format('yy.MM')
	linux {
		icon = 'util/linux/KoLmafia.ico'
	}
	mac {
		type = 'dmg'
		icon = 'util/macosx/limeglass.icns'

	}
	windows{
		type = 'exe'
		icon = 'util/windows/KoLmafia.ico'
		winShortcut = 'true'
		winDirChooser = 'true'
		javaOptions = ["-DuseCWDasROOT=true"]
	}
	doFirst {
		mainJar = tasks.shadowJar.archiveFileName.get()
		appVersion += '.' + version
		appName = 'KoLmafia-r' + version + "${versioning.info.dirty ? 'M' : ''}"
		println 'Packaging app ' + appName + '...'
		if (System.properties['os.name'] != 'Linux') {
			return
		}
		def hasDeb = exec {
			ignoreExitValue = true
			errorOutput = OutputStream.nullOutputStream()
			executable 'bash'
			args '-c', 'which dpkg-deb'
		}
		def hasRpm = exec {
			ignoreExitValue = true
			errorOutput = OutputStream.nullOutputStream()
			executable 'bash'
			args '-c', 'which rpmbuild'
		}
		if (hasDeb.getExitValue() == 0) {
			println 'Targeting deb for jpackage...'
			linux { type = 'deb' }
		} else if (hasRpm.getExitValue() == 0) {
			println 'Targeting rpm for jpackage...'
			linux { type = 'rpm' }
		} else {
			println 'Bundling all files for jpackage.'
			linux { type = 'app-image' }
		}
	}
}

clean.dependsOn cleanDist
pruneDist.dependsOn getRevision

// Note that pruneDist relies on getRevision.
jar.dependsOn pruneDist
shadowJar.dependsOn pruneDist

jacocoTestReport.dependsOn test

def lastRevision() {
	def revisionFile = file('build/revision.txt')
	return revisionFile.exists() ? revisionFile.text.trim() : '0'
}

// Set version from last build/revision.txt if up-to-date
version = lastRevision()
